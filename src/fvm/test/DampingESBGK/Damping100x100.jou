/ 2-D damping journal file

$mesh="Damping100x100.msh" 

/ Length and height of domain
$L=12.8571 
$H=6.4286

$Loff=$L/2.0
$Hoff=$H/2.0

/width and thickness of beam	
$w=6.4286 
$t=1.6071

/gap-size
$g=1.0

/mesh -interval count, successive ratio
$Nx1=50
$Nx2=50
$rx1=1
$rx2=1

$Ny1=25
$Ny2=25
$Ny3=50
$ry1=1
$ry2=1
$ry3=1


//Do Not Change
$xoff=$w/2.0
$yoff=$t/2.0+$g


/face create width $L height $H offset $Loff $Hoff 0 xyplane rectangle
vertex create "v1"  coordinates 0 0 0
vertex create "v2"  coordinates 0 $H 0
vertex create "v3"  coordinates $L $H 0
vertex create "v4"  coordinates $L 0 0

/face create width $w height $t offset $xoff $yoff 0 xyplane rectangle
vertex create "v5" coordinates 0 $g 0
$tpg=$g+$t
vertex create "v6" coordinates 0 $tpg 0
vertex create "v7" coordinates $w $tpg 0
vertex create "v8" coordinates $w $g 0

/extra vertices
vertex create "v9" coordinates $w 0 0
vertex create "v10" coordinates $w $H 0
vertex create "v11" coordinates $L $g 0
vertex create "v12" coordinates $L $tpg 0

/edge of beam
/edge create "e1" straight "v5" "v6"
edge create "e2" straight "v6" "v7"
edge create "e3" straight "v8" "v7"
edge create "e4" straight "v5" "v8"

/left edge of domain
edge create "e5" straight "v1" "v5"
edge create "e6" straight "v6" "v2"
/top
edge create "e7" straight "v2" "v10"
edge create "e8" straight "v10" "v3"
/bottom
edge create "e9" straight "v1" "v9"
edge create "e10" straight "v9" "v4"
/right
edge create "e11" straight "v4" "v11"
edge create "e12" straight "v11" "v12"
edge create "e13" straight "v12" "v3"

/mesh 
/beam width 
edge delete "e9" "e4" "e2" "e7" keepsettings onlymesh
edge picklink "e7" "e2" "e4" "e9"
edge mesh "e9" "e4" "e2" "e7" successive ratio1 $rx1 ratio2 $rx1 intervals $Nx1

/wall x-domain
edge picklink "e10" "e8"
edge mesh "e8" "e10" successive ratio1 $rx2 intervals $Nx2

/gap
edge picklink "e5" "e11" 
edge mesh "e5" "e11" successive ratio1 $ry1 intervals $Ny1

/thickness
edge picklink "e3" "e12" 
edge mesh "e3" "e12" successive ratio1 $ry2 intervals $Ny2

/symmetry y-domain
edge picklink "e6" "e13"
edge mesh "e6" "e13" successive ratio1 $ry3 intervals $Ny3

/create face
face create "face1" wireframe "e5" "e4" "e3" "e2" "e6" "e7" "e8" "e13" "e12" \
  "e11" "e10" "e9" real

/mesh
face mesh "face1" map size 1

/boundary conditions id  3 to 7 in reverse order in fluent 
physics create "beam_wall" btype "WALL" edge "e2" "e3" "e4"
physics create "substrate" btype "WALL" edge "e9" "e10"
physics create "rightPressureInlet" btype "PRESSURE_INLET" edge "e13" "e12" \
  "e11"
physics create "topPressureInlet" btype "PRESSURE_INLET" edge "e7" "e8"
physics create "symmetry" btype "SYMMETRY" edge "e5" "e6" 



physics create "gas" ctype "FLUID" face "face1"

export fluent5 $mesh nozval
